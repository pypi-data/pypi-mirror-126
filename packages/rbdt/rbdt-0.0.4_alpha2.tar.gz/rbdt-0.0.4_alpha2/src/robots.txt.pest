blank = _{"" ~ NEWLINE}

SPECIAL_CHARACTER = {"-" | "_" | "+" | "!" | " " | "*" | "." | "/" | "(" | ")"  | "[" | "]" | ";" | ":" | "'" | "&" | "," | "/" | "." | "?" | "=" | "$" | ":" | "%" | "`" | "\\" | "'" | "|" | "~" | "*" |"@" | "{" | "}" | "^" | "\"" | "<" | ">"}
specific_agent = {(ASCII_ALPHANUMERIC | SPECIAL_CHARACTER)+}
default_agent = {&("*" ~ NEWLINE) ~ "*"} //not totally sure about ** here, from salesforce.com
user_agent = {^"user-agent:" ~ (default_agent | specific_agent)? ~ NEWLINE}

url = {(ASCII_ALPHANUMERIC | SPECIAL_CHARACTER)*}
allow = {^"allow" ~ ":"? ~ url ~ NEWLINE}
disallow = {^"disallow" ~ ":"? ~ url ~ NEWLINE}
noindex = {^"noindex:" ~ url ~ NEWLINE}
number = { ('0' .. '9' | ".")+ }
host = {^"host:" ~ url ~ NEWLINE}
crawl_delay = {^"crawl-delay:" ~ number ~ NEWLINE}

sitemap = {^"sitemap" ~ ":" ~ url ~ NEWLINE}
video_sitemap = {^"video sitemap" ~ ":" ~ url ~ NEWLINE}

weirdos = {^"clean-param" | "DaumWebMasterTool" | ^"request-rate" | ("ACAP-" ~ ("crawler" | "disallow-crawl")) | ^"nosnippet" | ^"reference" | ^"host-load" | ^"noindex" | ^"Crawl-limit"}
weird_directive =  {weirdos ~ ":" ~ url ~ NEWLINE}

WHITESPACE = _{" "+ | "\t"}

block  = {host? ~ user_agent+ ~ (crawl_delay | allow | disallow | host | sitemap | video_sitemap | noindex | weird_directive )*}
section = _{(block | blank | sitemap | video_sitemap | crawl_delay)}
file = { SOI ~ (section)* ~ (url ~ NEWLINE)? ~ EOI }